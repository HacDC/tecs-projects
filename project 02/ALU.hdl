// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    And16(a=x, b=false, out=xzT);
    Mux16(a=x, b=xzT, sel=zx, out=xz);
    Not16(in=xz, out=xznT);
    Mux16(a=xz, b=xznT, sel=nx, out=xzn);
    And16(a=y, b=false, out=yzT);
    Mux16(a=y, b=yzT, sel=zy, out=yz);
    Not16(in=yz, out=yznT);
    Mux16(a=yz, b=yznT, sel=ny, out=yzn);
    And16(a=xzn, b=yzn, out=outAnd);
    Add16(a=xzn, b=yzn, out=outAdd);
    Mux16(a=outAnd, b=outAdd, sel=f, out=outF);
    Not16(in=outF, out=outN);
    Mux16(a=outF, b=outN, sel=no, out=out, out[0..7]=oA, out[8..15]=oB, out[15]=ng);
    Or8Way(in=oA, out=orA);
    Or8Way(in=oB, out=orB);
    Or(a=orA, b=orB, out=orC);
    Not(in=orC, out=zr);
}
